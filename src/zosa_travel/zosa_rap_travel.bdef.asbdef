managed;
with draft;
//strict;

define behavior for ZOSA_RAP_TRAVEL alias Travel
implementation in class zbp_osa_rap_travel unique
persistent table zosa_atrav
draft table zosa_dtrav
lock master total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{
  create;
  update;
  delete;
  association _Booking { create; with draft;}
  field ( numbering : managed, readonly ) TravelUuid;
  field ( readonly ) TravelId, TotalPrice, travelStatus;
  field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy, LocalLastChangedAt;
  field ( mandatory ) AgencyID, CustomerID;

  action ( features : instance ) acceptTravel result [1] $self;    //defining dynamic feature control
  action ( features : instance ) rejectTravel result [1] $self;    //defining dynamic feature control
  internal action RecalcTotalPrice;

  determination setInitialStatus on modify { create; }
  determination CalculateTotalPrice on modify { field BookingFee, CurrencyCode; }
  determination CalculateTravelID on save { create; }

  validation ValidationAgency on save { field AgencyId; create; }
  validation ValidateCustomer on save { field CustomerId; create; }
  validation ValidateDates on save { field BeginDate, EndDate; create; }

draft determine action Prepare {
validation ValidationAgency;
validation ValidateCustomer;
validation ValidateDates;
}

  mapping for zosa_atrav
  { TravelUuid = travel_uuid;
    TravelId = travel_id;
    AgencyId = agency_id;
    CustomerId = customer_id;
    BeginDate = begin_date;
    EndDate = end_date;
    BookingFee = booking_fee;
    TotalPrice = total_price;
    CurrencyCode = currency_code;
    Description = description;
    TravelStatus = overall_status;
    CreatedBy = created_by;
    CreatedAt = created_at;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }
}

define behavior for ZOSA_RAP_BOOKING alias Booking
implementation in class zbp_osa_rap_booking unique
persistent table zosa_abook
draft table zosa_dbook
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  association _Travel {with draft;}

  field ( numbering : managed, readonly ) BookingUuid;
  field ( readonly ) TravelUuid, BookingId;                         //static feature control
  field ( readonly ) CreatedBy, LastChangedBy, LocalLastChangedAt;  //static feature control

  determination CalculateBookingID on modify { create; }
  determination CalculateTotalPrice on modify { field FlightPrice, CurrencyCode; }




  mapping for zosa_abook
  {
    BookingUUID = booking_uuid;
    TravelUUID = travel_uuid;
    BookingID = booking_id;
    BookingDate = booking_date;
    CustomerID = customer_id;
    CarrierID = carrier_id;
    ConnectionID = connection_id;
    FlightDate = flight_date;
    FlightPrice = flight_price;
    CurrencyCode = currency_code;
    CreatedBy = created_by;
    LastChangedBy = last_changed_by;
    LocalLastChangedAt = local_last_changed_at;
  }
}